// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "./dependencies.gradle"
buildscript {
    ext.kotlin_version = "1.4.32"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.0-rc01"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenCentral()
    }
}

static boolean isJavaPlugin(Plugin plugin) {
    plugin.getClass().canonicalName in ["org.gradle.api.plugins.JavaPlugin", "org.gradle.api.plugins.JavaLibraryPlugin"]
}

static boolean isAndroidPlugin(Plugin plugin) {
    plugin.getClass().canonicalName in ["com.android.build.gradle.LibraryPlugin", "com.android.build.gradle.AppPlugin"]
}

subprojects { project ->

    project.plugins.whenPluginAdded { plugin ->

        if (isJavaPlugin(plugin)) {
            project.apply from: "$rootProject.projectDir/java_library.gradle"
        }

        if (isAndroidPlugin(plugin)) {
            project.apply from: "$rootProject.projectDir/android_library.gradle"
        }

        if (project.path.endsWith("api")) {
            if (isAndroidPlugin(plugin)) {
                project.apply from: "$rootProject.projectDir/feature_module_api.gradle"
            }
        }

        if (project.path.endsWith("impl")) {
            if (isAndroidPlugin(plugin)) {
                project.apply from: "$rootProject.projectDir/feature_module_impl.gradle"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}